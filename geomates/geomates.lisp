;;;
;;; clone of the 'geometry friends' competition
;;;
;;; This clone provides socket connections to agents and a web browser for GUI (graphics output only so far).
;;; World simulation is performed using a dynamic library that wraps calls to the box2d library (which only comes as static
;;; library, unfortunately). You need to build the wrapper library and add the path to your library at the beginning of this file.
;;; Be sure to leave existing "probe-files" untouched so this program will work in different environments. 
;;; Agent and GUI connect to the ports defined above. You may change the ports as you like. The assignment of agents
;;; to disc and rect player is performed randomly; each agent is informed which player it controls.
;;; Agents interact with the game by sending simple text data via the TCP/IP socket connection:
;;; a : move left
;;; d : move right
;;; w : jump (disc player only)
;;; w : strech horizontally (rect player only)
;;; s : compress horizonztally (rect player only)
;;; q : request to quit this level (quitting only occurs when both agents have requested to quit)
;;; m(...) : send message (...) in KIF format, i.e., a symbolic expression to other agent
;;;
;;; You can use "telnet" in the command line to connect to the game server and play an agent yourself.
;;; To this end, the game server sends some commands (according to RFC 854) to connected agent/terminal that
;;; switch a telnet client into byte mode in order to send a packet immediately after a key is pressed.
;;; In your agent implementation you should therefore ignore the first 6 bytes received in case your
;;; agent gets confused with receiving non-printable characters. Sending the telnet commands can be
;;; disabled in "await-agent-connections", but then you would need to configure telnet each time manually
;;; to switch to byte mode first in order to use it.
;;;
;;; Levels are defined in a global variable *levels* as list of levels. Each level consists out of objects
;;; given as s-expressions. The coordinate system is x to the right, positive  is up. Unit size is meter.
;;; Levels should be 80 x 40 in size to fit the graphical output. 
;;; Format and semantics are as follows:
;;; (:rect x y) starting position of the rect, the rect player starts with initial width according to +rect-length+ (2m)
;;; (:disc x y) starting position of the disc, the disc is of radius +disc-radius+ (1.0m)
;;; (:platform x1 y1 x2 y2) defines a rectangular platform with corners (x1 y1) and (x2 y2)
;;;                         be nice and add a ground plane and side walls to avoid your players falling off accidentally
;;; (:diamond x y) diamond to collect: each level must contain at least one diamand as a level is said to be finished if
;;;                no diamonds are left
;;; 

(eval-when (:execute :load-toplevel :compile-toplevel)
  (load "base64.lisp")
  (load "sha1.lisp")
  (load "levels.lisp")
  ;; (require :sb-alien) ;; not needed in modern SBCL (uncomment only if sb-alien module is unknown)
  (require :sb-bsd-sockets)  ;; for networking
  (require :sb-concurrency)) ;; for lock-free threadsafe queues


(defparameter *path-to-wrapper-library* (or (probe-file "wrapper.so")
					    (probe-file "wrapper.dll")
					    (probe-file "C:/DeepSeek_ICA_Agent/geomates"))
  "path to the shared library that wraps around the static box2d library")

(defparameter *agent-port* 45678
  "ports on which agents connect to the game")

(defparameter *gui-port* 8000
  "port on which the browser GUI connects to the game")

;;;
;;; Do not change parameters below except for your own experiments
;;;

(defparameter +rect-length+ 4.0f0
  "initial width of the rect shape as single float")

(defparameter +rect-force+ 30.0f0
  "force for moving rect (needs to be adjusted for different rect sizes)")

(defparameter +disc-radius+ 1.5f0
  "radius of the disc shape as single float")

(defparameter +disc-force+ 80.0f0
  "force for moving disc (needs to be adjusted for different disc sizes)")

(defparameter +announce-who-am-i+ t
  "whether or not to inform agents which character they are controlling")

(defun initialize-box2d ()
  (sb-alien:load-shared-object *path-to-wrapper-library*))

;; foreign function interface for linking to box2d wrapper
;; functions and types from C library
(define-alien-type pose (* (struct bodyPose (x float) (y float) (r float) (w float) (h float))))
(define-alien-routine "initWorld" void (gravity-x float) (gravity-y float))
(define-alien-routine "destroyWorld" void)
(define-alien-routine "initPlayers" void (rect-x float) (rect-y float) (rect-size float) (rect-ratio float) (rect-density float) (rect-friction float) (disc-x float) (disc-y float) (disc-size float) (disc-density float) (disc-friction float))
(define-alien-routine "worldInsertPlatform" void (pos-x float) (pos-y float) (size-x float) (size-y float))
(define-alien-routine "stepWorld" void)
(define-alien-routine "getRectPlayerPose" pose)
(define-alien-routine "getDiscPlayerPose" pose)
(define-alien-routine "moveDiscPlayer" void (force float))
(define-alien-routine "jumpDiscPlayer" void (force float))
(define-alien-routine "moveRectPlayer" void (force float))
(define-alien-routine "transformRectPlayer" void (dir float))
(define-alien-routine "pointInRectPlayer" int (x float) (y float))

(defun setup-level (level)
  "sets up the box2d simulation environment for a given level description, returns list of diamonds"
  (let ((disc (assoc :disc level))
	(rect (assoc :rect level))
	(platforms (remove-if-not #'(lambda (entry) (eql (first entry) :platform)) level))
	(diamonds  (mapcar #'(lambda (d) ; cast to single-floats for box2d 
			       (list :diamond (coerce (second d) 'float) (coerce (third d) 'float)))
			   (remove-if-not #'(lambda (entry) (eql (first entry) :diamond)) level))))
    ;; sanity checking
    (unless diamonds
      (error "no diamonds in level"))
    (initworld 0.0f0 -10.0f0)
    (initplayers (coerce (second rect) 'single-float) ; starting position rectangle x/y
		 (coerce (third rect) 'single-float)
		 (coerce +rect-length+ 'single-float)
		 4.0f0 ; ratio
		 1.0f0 ; density
		 0.1f0 ; friction
		 (coerce (second disc) 'single-float) ; starting position disc x/y
		 (coerce (third disc) 'single-float)
		 +disc-radius+ ; radius
		 1.0f0 ; density
		 0.3f0); friction
    (loop for (pf x1 y1 x2 y2) in platforms do
      (worldinsertplatform (coerce x1 'single-float) (coerce y1 'single-float) (coerce x2 'single-float) (coerce y2 'single-float)))
    (values diamonds platforms)))

(defun act-r-scene-format (scene-string)
  "Transforms the scene-string into the correct ACT-R format."
  (let ((scene (read-from-string scene-string))) ;; Convert string to list
    (setf scene
          (mapcar (lambda (item)
                    (case (car item)
                      (:RECT (destructuring-bind (x y w h r d) (cdr item)
                               `(:RECT SCREEN-X ,x SCREEN-Y ,y WIDTH ,w HEIGHT ,h ROTATION ,r DIAMONDS ,d)))
                      (:DISC (destructuring-bind (x y r d) (cdr item)
                               `(:DISC SCREEN-X ,x SCREEN-Y ,y RADIUS ,r DIAMONDS ,d)))
                      (:DIAMOND (destructuring-bind (x y) (cdr item)
                                 `(:DIAMOND SCREEN-X ,x SCREEN-Y ,y)))
                      (:PLATFORM (destructuring-bind (x1 y1 x2 y2) (cdr item)
                                  `(:PLATFORM SCREEN-X ,x1 SCREEN-Y ,y1 WIDTH ,(- x2 x1) HEIGHT ,(- y2 y1))))
                      (t item))) scene)) ;; Apply transformation to each item in the scene
    
    ;; Now convert the modified scene list back into a string
    (format nil "(狺)" scene))) ;; Format the list as a string
;;
;; TCP/IP connections to agents 
;;

(defun accept (socket)
  "Like socket-accept, but retry on EAGAIN."
  (loop (handler-case
            (return (sb-bsd-sockets:socket-accept socket))
          (sb-bsd-sockets:interrupted-error ()))))

(defun await-agent-connections (port &optional (telnet2character-mode t))
  "returns two streams once two agent have connected to the given TCP port"
  (let ((socket (make-instance 'sb-bsd-sockets:inet-socket :type :stream :protocol :tcp)))
    (setf (sb-bsd-sockets:sockopt-tcp-nodelay socket)   t
	  (sb-bsd-sockets:sockopt-reuse-address socket) t)
    (sb-bsd-sockets:socket-bind socket #(127 0 0 1) port)
    (sb-bsd-sockets:socket-listen socket 3)
    (let ((stream (sb-bsd-sockets:socket-make-stream (accept socket) :input t :output t :element-type :default))
	  (stream2 (sb-bsd-sockets:socket-make-stream (accept socket) :input t :output t :element-type :default)))
      (when telnet2character-mode
	(loop for x in '(255 253 34 255 251 1) do (write-byte x stream)) ; switch telnet to character mode
	(loop for x in '(255 253 34 255 251 1) do (write-byte x stream2)) ; switch telnet to character mode
	(finish-output stream)
	(finish-output stream2))
      (values stream stream2 socket))))

(defun slurp-input (input-stream)
  "swallows all pending data"
  (loop while (listen input-stream) do (read-byte input-stream)))

;;
;; WebSocket for the GUI
;;
;; To ease installation, no WebSocket library is used but a bare-bones implementation instead
;; for better approaches see, e.g., https://lispcookbook.github.io/cl-cookbook/websockets.html
;;
;; This code relies on the SHA1 and base64 implementation provided at
;; https://github.com/massung/sha1/blob/master/README.md
;; the base64 and sha1 package must be loaded

(defparameter +web-socket-magick+ "258EAFA5-E914-47DA-95CA-C5AB0DC85B11"
  "the magic string constant for WebSocket authentification")

(defparameter *gui-thread* nil
  "the thread that serves the GUI")

(defparameter *gui-running?* t
  "flag to tell gui server to shut down")

(defparameter *gui-connected?* nil
  "boolean flag telling whether a GUI connection is active")

(defparameter *gui-view-queue* (sb-concurrency:make-queue)
  "level data to be send to the GUI")

(defparameter *gui-command-queue* (sb-concurrency:make-queue)
  "commands received from the GUI")

(defun gui-handler (port)
  "infinitely loop to handle incoming GUI connections"
  (setf *gui-thread* sb-thread:*current-thread*
	*gui-running?* t)
  (let ((socket (make-instance 'sb-bsd-sockets:inet-socket :type :stream :protocol :tcp)))
    (setf (sb-bsd-sockets:sockopt-tcp-nodelay socket)   t
	  (sb-bsd-sockets:sockopt-reuse-address socket) t)
    (sb-bsd-sockets:socket-bind socket #(127 0 0 1) port)
    (loop while *gui-running?* finally (sb-bsd-sockets:socket-close socket) do
      (setf *gui-connected?* nil)
      (sb-bsd-sockets:socket-listen socket 3)
      (let ((stream (sb-bsd-sockets:socket-make-stream (accept socket) :input t :output t :element-type :default))
	    (key nil))
	;; wait until connection request is received
	(format *standard-output* "&Connection request by GUI on port d.%" port)
	;; scan input for web socket key
	(loop until key do 
	  (loop while (and (listen stream)) do
	    (let ((line (read-line stream nil "" nil)))
	      (when (and (< 17 (length line))
			 (string= "Sec-WebSocket-Key:" line :end1 17 :end2 17))
		(setq key (subseq line 19 (- (length line) 1)))
		(format *standard-output* "request with key 'a'%" key))))
	  (sleep 1))
	;; reply to client
	(let ((reply-code (sha1:sha1-base64 (format nil "aa" key +web-socket-magick+) #'base64:base64-encode)))
	  (format  stream "HTTP/1.1 101 Switching ProtocolsaaConnection: UpgradeaaUpgrade: websocketaaSec-WebSocket-Accept: aaaaa"
		   #\Return #\Newline #\Return #\Newline #\Return #\Newline
		   reply-code
		   #\Return #\Newline #\Return #\Newline))
	(finish-output stream)
	(format *standard-output* "&send handshake%")
	(setf *gui-connected?* t)

	(loop with eof? = nil
	      until (or eof? (not *gui-running?*))
	      finally (progn (close stream)
			     (format *standard-output* "&GUI client hang-up%"))
	      do
		 ;; interact with gui
		 ;; something to send?
		 (unless (sb-concurrency:queue-empty-p *gui-view-queue*)
		   (let* ((data (sb-concurrency:dequeue *gui-view-queue*))
			  (dlen (length data))
			  frame-header)
		     (cond ((< dlen 126) ; short message?
			    (setf frame-header (list 129 dlen)))
			   ((< dlen 65536)
			    (setf frame-header (list 129 126 (ash dlen -8) (logand dlen 255))))
			   (t ;; FIXME: sending frames over 64K
			    (error "sending data frames over 64KB size not implemented")))
		     ;; send frame header and data
		     ;(format *standard-output* "&sending to GUI:a (d bytes)%" data dlen)
		     (loop for byte in frame-header do
		       (write-byte byte stream))
		     (loop for char across data do
		       (write-byte (char-code char) stream))
		     (finish-output stream)))
		 ;; something to receive?
		 (when (listen stream)
		   (let ((frame-header (read-byte stream nil :eof))
			 (frame-info   (read-byte stream nil :eof)))
		     (if (or (eql frame-header :eof)
			     (eql frame-info :eof))
			 (setf eof? t)
			 (let ((len (logand frame-info 127)))
			   ;; FIXME: we don't accept larger messages, i.e., len=126/127 with length in separate field, see https://en.wikipedia.org/wiki/WebSocket#Frame-based_message
			   (let ((masking-key (loop for i from 1 to 4 collect (read-byte stream nil :eof)))
				 (payload (loop for i from 1 to len collect (read-byte stream nil :eof))))
			     (if (or (find :eof payload)
				     (find :eof masking-key))
				 (setf eof? t)
				 (let ((msg (loop for data in payload
						  for index from 1
						  collect (code-char (logxor data (nth (mod index 4) masking-key))))))
				   (format *standard-output* "received from GUI:狺蜥骱狺ア眢扉篝骝犴瀛桢徜弪骝犴瀛轭骘疳祜徜磲箅轭绛脲┅┅┅┅┅┅ㄤ彐躅篝狎舡珲榄泔铑邈糸镱ī㈠篝徕扉箬弩族庥镢脲泔眄躅殂狒轱鏖翳翳怛秣箦钦散ㄦ矧磲篝犷溽蜾秕麴豸ヌ轶翦铋铉骘钦镱痫螋洚ア珲榄痫螋筲翳蝈徜喉犭瀛翳蝈徜灬礅溽īㄧ蹰栳钿戾珲榄痫螋┅┅换换澡玑礤祜镳轶蜥翳弪篝蜥殓梏骘蝼狎洚族蝓翳钦箫汶弭泔铑邈糸镱轭溴溟汜翦翳蝈徜箫麇滹瞌铄邃麸黠蝌换麒弭桢钦轶徙趱犰禊泔铑邈翦矧铒舢深翳磲轭祜镳麇麽轸骘翳赭徵孱趔麸泔铑邈犷翳孱祜镳翳蝻蹒换弼弪戾鲥鏖翳爱箦泔钿溴灬忮赭邋躔溽糸铉翳黠蜢洚涉犰溟犴镱潴狎泔祆邈翦洮翳铄戾鲥轶徙糸鲠翦洚换阻孱麇蝓秕镦戾鲥祗翳玑礤孱潴犷泔铑邈糸镱狎翦蝽轭狒邃换ㄤ彐躅磲轭ī㈨衢祜镳镦玑礤箦蝣弪箦翩蜥钿镯篝狒濯磲脲蜥钿镯篝狒舂ㄩ铋糸犰辁瀛怙蹭篝狎舡珲榄泔铑邈糸镱换麽轸骘徵孱趔麸泔铑邈戾è痫螋徵孱舡痫螋ㄤ獒盹钿蟓溟筱癌ㄤ獒盹钿蟓蝈泗癌ㄦ矧磲ψ衢糸铉骘徵孱趔麸泔铑邈镱痫螋洚ア痫螋戾舄ㄤ轶悱徵孱舡篝蝈犴蝈泗徵孱舡篝蝈犴轭骘篝蝈犴躅鏖钿痱雉邈眭祠轲戾鲠祯瀛忾钿翥瓠篝蝈犴翥瓠篝蝈犴徵孱舡箫汶弭ㄡ麽轸徵孱舡泔铑邈糸镱痫螋麽轸躅糸徵孱趔栳鲥泔铑邈翦换蜥钿镯禊狍箝珙溟筱犷蝈泗痨狴弪麒孱蜥钿镯博蝻翎翦翥瓠篝蝈犴翥瓠篝蝈犴博箦赳溟筱徵孱舡篝蝈犴翥瓠篝蝈犴蝈泗徵孱舡篝蝈犴翥瓠篝蝈犴轭骘篝蝈犴磲脲怛镝溷狍舡篝蝈犴溟筱徵孱舡篝蝈犴蝈泗徵孱舡篝蝈犴篝犷溽蜾秕麴豸┅换痨狴犰戾鲥祗祜镳骘戾鲥轭戾鲥祗骈钺祆痱镧ㄦ矧磲轭骘篝蝈犴㈠钿アㄦ轭轶璀秕麴豸轭骘篝蝈犴ㄣ祜箦溟筱徵孱舡篝蝈犴ㄣ祜箦蝈泗徵孱舡篝蝈犴筲怏洵箫汶弭蠛箫汶弭沆矬徵孱舡箫汶弭箦赳珲榄蝓铑轭缈铋飑ㄦ矧磲篝犷溽蜾秕麴豸溟筱溟犴镱濑盒ヲ邈艉溟犴镱濑盒ア溟犴镱潴溟筱溟犴镱潴蝈泗┅滹箪躜瓠轭瘐溟筱徵孱舡篝蝈犴箪躜瓠轭瘐蝈泗徵孱舡篝蝈犴戾è蝈泗徕矧趔铋飑麒弭桢徵孱趔栳鲥蝈聃弩翦麸徕矧翳轶戾鲥ㄤ轶悱徕矧趔铋飑礤篌徵瀛骝镯蝈泗铋飑礤篌徵弩徵孱趔鏖箬麸屮汨犷珏礤篌徵瀛骝镯溟筱铋飑蝈泗扉篝孱罂铋飑麒弭桢翳徵孱栳箦钿箫礤溽翎箫轸箬秕熹忮蝈徜麸扉篝孱狍麇祆ㄤ轶悱扉篝孱罂铋飑眭祠轲戾鲠祯瀛忾钿ㄤ獒盹钿痨狒骘蝽螬箦趱瓠戾鲥戾鲥飑换轭骘蝽徵孱趔麒殂痨狴弪翳妁泔铘蝻飕溴疱钿轭镱泔铈殓ㄩ犷铒躅沐麒锃犴楂痱镧ㄦ矧磲溟筱徵孱舡篝蝈犴ê痨狴轭溟筱狺幄＼义趱蝾＼五黛轭濠ㄦ矧磲蝈泗徵孱舡篝蝈犴ê痨狴轭蝈泗狺幄＼义趱蝾＼五黛轭濠痱镧ㄦ矧磲溟筱徵孱舡篝蝈犴ê痨狴轭躅腩秣瞟狺幄＼义趱蝾＼五黛轭濠ㄦ矧磲蝈泗徵孱舡篝蝈犴ê痨狴轭躅腩秣瞟狺幄＼义趱蝾＼五黛轭濠┅ㄦ轭轶璀秕麴豸溟筱徵孱舡篝蝈犴ㄦ轭轶璀秕麴豸蝈泗徵孱舡篝蝈犴换痨翳戾鲥祜镳麒殪ㄡ钿溟犴镱潴铒ㄡ钿蝈泗徕矧趔溟筱徕矧趔咯┅骈钺祆痱镧ㄦ矧磲轭骘篝蝈犴㈧弼屐骈铋箬邃狺幄＼义趱蝾＼五黛轭濠ㄦ轭轶璀秕麴豸轭骘篝蝈犴ㄤ弩趄稆黠蜢洎滹换狒翦钿麸溟筱徵孱花骘蝽狒溟筱徵孱扉篝孱轭绾幄扉篝孱溟筱徵孱舡篝蝈犴┅换铒翦篝蝈犴狎徙沐篌邃怡翦鏖箦铒轭汨狎徙翦盹溴麸狯镩镤溟糸弩镦赵骗溴泔溴痱镡戾眢换轭汜箦怛镫孱赵镢翦箦聃孱沐鏖祆忮蝈沐轹邃麒殂鏖祆忮翳汜箦轭汜箦镦忮轭泔铑邈翦换麸箫礤翦祛弭翳狒箦钿翦祛弭痱雉镢镬篝蹑麒孱扉篝孱溟筱徵孱舡篝蝈犴戾è汨ㄩ珙矧瀛弪蝻蝮蝈徜怡翦溟筱徵孱舡篝蝈犴┅┅箦赳溟筱扉篝孱罂舂ㄣ狍汨ü盹鲥拈筱徐狴弪诞版癌ū鞍盹鲥拈筱徐狴弪诞版癌ū惫牾眇拈筱徐狴弪溟筱骘蜚瀚┅换礤篌徵弩珏狃疱钿邃轭矧溴麸磲脲篚蝈翳妁鏖祆忮溴扉鲥蝈洮铒秭弪蜷滗邃怡铄黛蝈沐轹邃镱瀹燥翳轶孱洮礤篌徵瀛骝镯溟筱珏趔蝈箦麸紊徭翦溴扉鲥蝙麸蝈泗徵孱骢螋桢忮祜ū肮箦赳礤篌徵瀛骝镯溟筱ㄡ痧孱礤篌徵瀛骝镯溟筱蝈徜溟筱徵孱舡篝蝈犴铋铋铋飑┅悫ū背箦赳溟筱徕矧趔舂┅┅换狒翦钿麸蝈泗徵孱花骘蝽狒蝈泗徵孱扉篝孱轭绾幄扉篝孱蝈泗徵孱舡篝蝈犴┅麒孱扉篝孱蝈泗徵孱舡篝蝈犴麇蝈徜蜥怡翦麸狯镩痱镡戾眢轭赵溴泔溟铉轭汜箦麇蝈沐轹殪戾玑泔溴戾è汨ㄩ珙矧瀛弪蝻蝮蝈徜怡翦蝈泗徵孱舡篝蝈犴┅┅箦赳蝈泗扉篝孱罂舂ㄣ狍汨ü盹鲥义泗徐狴弪ō蝈泗骘蜚瀚┅ū鞍盹鲥义泗徐狴弪蝈泗骘蜚瀚┅ū钡趄犷箧矧硪邈粜灬弪爱辨癌ū惫趄犷箧矧硪邈粜灬弪爱辨癌ū肮箦赳礤篌徵瀛骝镯蝈泗ㄡ痧孱礤篌徵瀛骝镯蝈泗蝈徜蝈泗徵孱舡篝蝈犴铋铋铋飑┅悫箦铒翦镱礤篌徵弩徕秭ū背箦赳蝈泗徕矧趔舂┅┅换篝屦箝眭灬糸镱犷痫篝躔溽翦麸徵孱趔戾ㄤ轶悱痫蟓溟筱痫蟓蝈泗痫蟓蝈泗痫蟓蝈泗蝻翎糸镱蝈泗鏖漪蝈泗桢殓梏ㄤ雉轫弩ㄩ订丢悲栋爱斌ㄩ珙矧瀛弪蝻蝮篝屦黠蜢洎繇骈骘怙蹭泸狍桢换蝈徜痫箦骝镯怙蹭戾è痫箦篝蝓泗ㄤ弪彐ㄧ弭拈筱徐狴弪酗箦┅┅箦赳溟筱痫蟓箪雉痫箦篝蝓泗溟筱痫蟓箪雉痫箦篝蝓泗┅戾è痫箦篝蝓泗ㄤ弪彐ㄧ弭义泗徐狴弪酗箦┅┅箦赳蝈泗痫蟓箪雉痫箦篝蝓泗蝈泗痫蟓箪雉痫箦篝蝓泗蝈泗蝻翎糸镱箪雉痫箦篝蝓泗颟蝈泗鏖漪箪雉痫箦篝蝓泗鳗蝈泗桢殓梏箪雉痫箦篝蝓泗ц┅换汨邈骘溟犴镱潴翎脲戾èㄦ轭洵殒＇灬礅溽ㄤ伯ㄡ怏ōㄣ镯痨屮箦泔钿洎翳轵洎ㄣ镯痨屮溟筱痫蟓溟筱痫蟓┅┅溟犴镱潴┅麒孱箦赳溟犴镱潴ㄤ屐弭溟犴镱潴┅ㄩ钽溟犴镱潴溟筱┅戾èㄦ轭洵殒＇灬礅溽ㄤ痫轭羯钜邈粜灬弪箦泔钿洎翳轵洎┅溟犴镱潴┅麒孱箦赳溟犴镱潴ㄤ屐弭溟犴镱潴┅ㄩ钽溟犴镱潴蝈泗┅┅换箦钿沲蝌孱筱孱麸犷镱扉篝孱轭戾舄è痱轭舡痱弭豉铋飑ㄣ躜蝈铘筱孱ㄦ矧磲铋è阂琶叉叉叉叉存洎ê纳用叉叉叉洎狺狺齄齄蝈泗痫蟓蝈泗痫蟓蝈泗鏖漪蝈泗桢殓梏蝈泗蝻翎糸镱溟犴镱潴蝈泗溟筱痫蟓溟筱痫蟓溟筱蜥溟躞溟犴镱潴溟筱ㄩ礤篌徵瀛骝镯溟筱扉篝喉箸掘邈礤篌徵瀛骝镯溟筱ㄩ礤篌徵瀛骝镯蝈泗扉篝喉箸句轶礤篌徵瀛骝镯蝈泗溟犴镱潴痨狒骘蝽螬┅麒孱珲榄泔铑邈翦淇麸钦ㄩ镱轶泔铑邈翦洎筲泔钽躜蝈钽哄铖蹂蹂沲蝌孱舡筱孱珲榄鲩鬻聃艴濯┅麒孱蝈泗扉篝孱罂麸蝈泗徵孱殒轸栳箦钿箫礤泔眄犷黩轸瀛扉铄沲蝌孱舡筱孱蝈泗徵孱舡篝蝈犴ㄦ轭轶璀秕麴豸蝈泗徵孱舡篝蝈犴箦赳蝈泗扉篝孱罂铋礤篌徵瀛骝镯溟筱铋飑麒孱溟筱扉篝孱罂麸溟筱徵孱殒轸栳箦钿箫礤泔眄犷黩轸瀛扉铄沲蝌孱舡筱孱溟筱徵孱舡篝蝈犴ㄦ轭轶璀秕麴豸溟筱徵孱舡篝蝈犴箦赳溟筱扉篝孱罂铋礤篌徵瀛骝镯蝈泗铋飑┅换栳钿扉铉蝈聃弩趔骝镯钦躅戾篌筲泔钽躜蝈钽厚蹂蹂屙痿珲榄泔眄犷洵聃艴濯戾è泔眄犷筲泔钽躜蝈钽轰羼蹂蹂珲榄泔眄犷洵聃艴濯┅ㄦ矧磲篝犷溽蜾秕麴豸Ⅱ邈彘鲥钦泔眄犷岙ア泔眄犷洎┅箪邋爱暴┅┅换沆遽瞽躔镱屮轸ㄣ祜箦溟筱徵孱舡篝蝈犴ㄣ祜箦蝈泗徵孱舡篝蝈犴箦赳珲榄蝓铑轭缈铋飑箦骒徵镦钦箦蝣弪麸屮轸轰镱濠┅ㄥ鲠飙麒孱ê屮邈豸红镝洵麸痨弼屐磲轭┅